external procedure LogProcTime(string,longint);

SetLangMode(LangRussian,"RUS",0);


global
updating function LongInt GlobalGiftVaucherVcRecordCheck(var record GlobalGiftVaucherVc GGVr,record GlobalGiftVaucherVc GGV2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record GlobalGiftVaucherVc oldGGVr;
  boolean foundf,TrHs;
  longint curtick;
	
	curtick = getcurtick();
  logtext(0,"GlobalGiftVaucherVcRecordCheck");
  
  res = 0;
  
  if(GGVr.SerNr<0)then begin 
  	GGVr.SerNr = NextSerNr("GlobalGiftVaucherVc",CurrentDate,-1,false,"");  
  end;
  
  if(blank(GGVr.BarCode))then begin
  	RecordCheckError(1854,"",-1,"BarCode");      
		res = -1;
		goto L99GlobalGiftVaucherVcRecordCheck; 
  end;
  if(blank(GGVr.Program))then begin
  	RecordCheckError(1854,"",-1,"Program");      
		res = -1;
		goto L99GlobalGiftVaucherVcRecordCheck; 
  end;
  
  oldGGVr.BarCode = GGVr.BarCode;
  oldGGVr.Program = GGVr.Program;
  foundf = false;
  TrHs = true;
  while(loopkey("BarCode",oldGGVr,2,TrHs))begin
  	if(oldGGVr.BarCode!=GGVr.BarCode)then begin TrHs = false; end;
  	if(oldGGVr.Program!=GGVr.Program)then begin TrHs = false; end;
  	
  	if(TrHs and oldGGVr.SerNr!=GGVr.SerNr)then begin      
      RecordCheckError(35116,"",-1,"BarCode");      
      res = -1;
      goto L99GlobalGiftVaucherVcRecordCheck; 
  	end;
  end;
  
	L99GlobalGiftVaucherVcRecordCheck:;
  GlobalGiftVaucherVcRecordCheck = res;
	LogProcTime("GlobalGiftVaucherVcRecordCheck",getcurtick()-curtick); 
return;
end;


global updating procedure CreateGlobalCardsMn(record RcVc RepSpec)
begin
	record GlobalGiftVaucherVc GGVr,oldGGVr;
	integer i;
	string 255 resbarcode,pin;
	longint curtick;
	
	curtick = getcurtick();
	For(i=0;i<RepSpec.flags[1];i=i+1) begin
StepB:;
	  resbarcode = random(1000,9999) & random(1000,9999) & random(1000,9999);
	  pin = random(1000,9999);
	  oldGGVr.BarCode = resbarcode;
	  if(readfirstkey("BarCode",oldGGVr,1,true)==false)then begin
	  	recordnew(GGVr);
	  	GGVr.SerNr = NextSerNr("GlobalGiftVaucherVc",CurrentDate,-1,false,"");  
	  	GGVr.Program = "FIRST";
	  	GGVr.BarCode = resbarcode;
	  	GGVr.CreateDate = currentdate;
	  	GGVr.PinCode = pin;
	  	recordstore(GGVr,true);
	  end else begin
	  	goto StepB;
	  end;
	end; 

	LogProcTime("CreateGlobalCardsMn",getcurtick() - curtick);
return;
end;



global procedure ListCreatedGVRn(record RcVc RepSpec)
begin
	record GlobalGiftVaucherVc GGVr;
	boolean TrHs;
	longint curtick;
	
	curtick = getcurtick();
	GGVr.CreateDate = RepSpec.d1;
	StartReportnoheaderjob("Список созданных подарочных карт");
	startformat(15);
		outstring(0,0,"Штрих-код",false);
		outstring(70,0,"PIN",false);
		outstring(120,0,"Дата создания",false);
	endformat;
	TrHs = true;
	while(loopkey("CreateDate",GGVr,1,TrHs))begin
		if(GGVr.CreateDate!=RepSpec.d1)then begin
			TrHs = false;
		end;
		
		if(TrHs)then begin
			startformat(15);
				outstring(0,0,GGVr.BarCode,false);
				outstring(70,0,GGVr.PinCode,false);
				outstring(120,0,GGVr.CreateDate,false);
			endformat;
		end;
	end;
	endjob;
	LogProcTime("ListCreatedGVRn",getcurtick()-curtick);
return;
end;

global procedure StatusGVRn(record RcVc RepSpec)
begin
	record GlobalGiftVaucherVc GGVr;
	boolean TrHs;
	longint curtick;
	
	curtick = getcurtick();
	GGVr.BarCode = RepSpec.f1;
	StartReportnoheaderjob("Статус подарочных карт");
	startformat(15);
		outstring(0,0,"Штрих-код",false);
		outstring(70,0,"PIN",false);
		outstring(120,0,"Дата создания",false);
		outstring(160,0,"Дата старта",false);
		outstring(200,0,"Дата окончания",false);
		outstring(240,0,"Баланс",false);
	endformat;
	TrHs = true;
	while(loopkey("BarCode",GGVr,1,TrHs))begin
		if(nonblank(RepSpec.f1) and GGVr.BarCode!=RepSpec.f1)then begin
			TrHs = false;
		end;
		
		if(TrHs)then begin
			startformat(15);
				outstring(0,0,GGVr.BarCode,false);
				outstring(70,0,GGVr.PinCode,false);
				outstring(120,0,GGVr.CreateDate,false);
				outstring(160,0,GGVr.StartDate,false);
				outstring(200,0,GGVr.EndDate,false);
				outstring(240,0,GGVr.Bal,false);
			endformat;
		end;
	end;
	endjob;
	LogProcTime("StatusGVRn",getcurtick()-curtick);
return;
end;