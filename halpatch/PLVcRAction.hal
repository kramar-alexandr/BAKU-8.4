//server-only
external updating procedure SiteMinder_Update_PLVc(record PLVc,record PLVc);
external procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external procedure B1ToB2Val(val,val,val,var val);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external function Boolean GetAccName(string,var string,Integer);
external function Boolean IsVATCodeDefined(string);
external procedure ExtractObj(string,var Integer,var string);// Edit ************************** Saturday, 19 August 2017 12:04:24
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external procedure LogProcTime(string,longint);
external procedure ExtractObjWithSeparator(string,string,Boolean,var Integer,var string);
external updating procedure DateOfCheckAndPriceChange; // Edit ************************** Ihor Trubachov 21*05*2021

SetLangMode(LangRussian,"RUS",0); // Edit ************************** Saturday, 19 August 2017 12:04:25



global updating procedure AddPLHist(record PLVc PLr,record PLVc PL2r)// Edit ************************** Saturday, 19 August 2017 12:04:26
begin
	record PLHistVc PLHr;
	record PLHistSetBlock PLHSb;
	
	blockload(PLHSb);
	if(PLHSb.DoHist>0)then begin
		PLHr.ArtCode = PLr.ArtCode;
		PLHr.PLCode = PLr.PLCode;
		PLHr.CustCode = PLr.CustCode;
		PLHr.SerialNr = PLr.SerialNr;
		if(readfirstmain(PLHr,4,true))then begin
			if(PLr.ExVatPrice!=PLHr.LastPrice or PLr.CurncyCode!=PLHr.LastCurncyCode)then begin
				PLHr.LastPrice = PLr.ExVatPrice;
				PLHr.LastCurncyCode = PLr.CurncyCode;
				PLHr.TransDate = currentdate;
				PLHr.TransTime = currenttime;
				recordstore(PLHr,true);
			end;
		end else begin
			recordnew(PLHr);
			PLHr.ArtCode = PLr.ArtCode;
			PLHr.PLCode = PLr.PLCode;
			PLHr.CustCode = PLr.CustCode;
			PLHr.SerialNr = PLr.SerialNr;
			PLHr.TransDate = currentdate;
			PLHr.TransTime = currenttime;
			PLHr.LastPrice = PLr.ExVatPrice;
			PLHr.LastCurncyCode = PLr.CurncyCode;
			recordstore(PLHr,true);
		end;
		
	end;
	
return;
end;

global updating procedure UpdatePrice(record PLVc PLr)// Edit ************************** Saturday, 19 August 2017 12:04:26
begin
  record GlobalItemVc GIr;
	record INVc INr;
	string 255 Code;
	val fr,to1,to2,br1,br2;
	string 5 cur;
	roundmode rnd;

	AddPLHist(PLr,PLr);// Edit ************************** BPI Ukraine - KramarAlexandr - 01, 21 10 2019 y. at 3:38:01 PM
	
	/*INr.Code=PLr.ArtCode;
	if(ReadFirstMain(INr,1,true)) then begin
		Code="";
		if(nonblank(INr.BPIBrand)) then begin
			Code=INr.BPIBrand & "_" & INr.Code;
		end;
	end;*/
	/* GIr.Code=Code;
	if(ReadFirstMain(GIr,1,true)) then begin
		cur=PLr.CurncyCode;
		GetFullCurncyRate(cur,CurrentDate,fr,to1,to2,br1,br2);
		GIr.Price = MulRateToBase1(cur,PLr.ExVatPrice,fr,to1,to2,br1,br2,rnd);
		RecordStore(GIr,true);
	end;	*/	
return;
end;


global
updating function LongInt PLVcRecordCheck(var record PLVc PLr,record PLVc PL2r,LongInt stat,LongInt long4)// Edit ************************** Saturday, 19 August 2017 12:04:41
begin
  record INVc INr;
  record CUVc CUr;
  record PLDefVc PLDefr;
  LongInt res;
  string 255 tstr;
  longint curtick;
	
	curtick = getcurtick();
  logtext(0,"PLVcRecordCheck");    
  
  res = 0;
  if blank(PLr.ArtCode) then begin
    RecordCheckError(1058,"",-1,"ArtCode");      
    res = -1; 
    goto LPLVcRecordCheck;
  end else begin
    if (ReadFirstItem(PLr.ArtCode,INr,true,false)==false) then begin
      RecordCheckError(1120,PLr.ArtCode,-1,"ArtCode");      
      res = -1; 
      goto LPLVcRecordCheck;
    end;
  end;
  if (blank(PLr.PLCode)) and (blank(PLr.CustCode)) then begin
    RecordCheckError(1058,"",-1,"PLCode");      
    res = -1; 
    goto LPLVcRecordCheck;
  end else begin
    if nonblank(PLr.PLCode) then begin
      PLDefr.Code = PLr.PLCode;
      if (ReadFirstMain(PLDefr,1,true)==false) then begin
        RecordCheckError(1120,PLr.PLCode,-1,"PLCode");      
        res = -1; 
        goto LPLVcRecordCheck;
      end;
    end;
    if nonblank(PLr.CustCode) then begin
      CUr.Code = PLr.CustCode;
      if (ReadFirstMain(CUr,1,true)==false) then begin
        RecordCheckError(25601,PLr.CustCode,-1,"CustCode");
        res = -1; 
        goto LPLVcRecordCheck;
      end;
    end;
  end;
  if nonblank(PLr.SalesAcc) then begin
    if (GetAccName(PLr.SalesAcc,tstr,60)==false) then begin
      RecordCheckError(1931,PLr.SalesAcc,-1,"SalesAcc");      
      res = -1; 
      goto LPLVcRecordCheck;
    end;
  end;
  if nonblank(PLr.VATCode) then begin
    if (IsVATCodeDefined(PLr.VATCode)==false) then begin
      RecordCheckError(1120,PLr.VATCode,-1,"VATCode");      
      res = -1; 
      goto LPLVcRecordCheck;
    end;
  end;
  if nonblank(PLr.BBVarAcc) then begin
    if (GetAccName(PLr.BBVarAcc,tstr,60)==false) then begin
      RecordCheckError(1007,PLr.BBVarAcc,-1,"BBVarAcc");      
      res = -1; 
      goto LPLVcRecordCheck;
    end;
  end;
  AddPLHist(PLr,PL2r);// Edit ************************** Thursday, 23 May 2013 17:19:49
	//UpdatePrice(PLr);
LPLVcRecordCheck:;
  PLVcRecordCheck = res;
	LogProcTime("PLVcRecordCheck",getcurtick()-curtick); 
  return;
end;

global
updating function LongInt PLVcRecordSaveAfter(var record PLVc PLr,record PLVc PL2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  SiteMinder_Update_PLVc(PLr,PL2r);
  PLVcRecordSaveAfter = res;
  return;
end;

global
updating function LongInt PLVcRecordUpdateAfter(var record PLVc PLr,record PLVc PL2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  SiteMinder_Update_PLVc(PLr,PL2r);
  PLVcRecordUpdateAfter = res;
  return;
end;

global
updating function LongInt PLVcRecordImport(var record PLVc PLr,record PLVc PL2r,LongInt stat,LongInt long4)// Edit ************************** Saturday, 19 August 2017 12:06:46
BEGIN
  LongInt res;
  val t,fr,to1,to2,br1,br2;
  string 5 curncy;
  Boolean gBase1ToBase2,testf;
  record ConvMasterBlock cvm;
  record PLDefVc PLDefr;
  record BaseCurBlock bcur;
	record PLVc PL3r; 
	 
  BlockLoad(cvm);
  if (cvm.Base1ToBase2Flag!=0) then begin gBase1ToBase2 = true; end;
  if (gBase1ToBase2) then begin
    testf = true;
    BlockLoad(bcur);
    PLDefr.Code = PLr.PLCode;
    if (ReadFirstMain(PLDefr,1,true)) then begin
      if ((nonblank(PLDefr.CurncyCode))) then begin//and (PLDefr.CurncyCode!=bcur.BaseCur2), if it is in curenncy we should recalc
        testf = false;
      end;
    end;
    if (testf) then begin
      GetFullCurncyRate(curncy,CurrentDate,fr,to1,to2,br1,br2);
      B1ToB2Val(PLr.ExVatPrice,br1,br2,t);
      PLr.ExVatPrice = t;
    end;
  end;
  
  PL3r.ArtCode = PLr.ArtCode;
	PL3r.PLCode = PLr.PLCode;
	PL3r.CustCode = PLr.CustCode;
	PL3r.SerialNr = PLr.SerialNr;
	if(readfirstmain(PL3r,4,true))then begin
	
	end else begin
		recordnew(PL3r);
	end;
  AddPLHist(PLr,PL3r);// Edit ************************** Thursday, 23 May 2013 17:19:49
  
  PLVcRecordImport = res;
  RETURN;
END;

global updating procedure DelPLHistMn(record RcVc RepSpec)
begin
	record PLHistVc PLHr;
	longint curtick;
	
	curtick = getcurtick();
	while(loopmain(PLHr,1,true))begin
		recorddelete(PLHr);
		stepback(PLHr);
	end;
	LogProcTime("DelPLHistMn",getcurtick()-curtick); 
return;
end;

global 
function Boolean PLVcRecordShouldBeSynchronised(record PLVc PLr,string tagstr)
begin
  Boolean res;
  
  res = true;
  PLVcrecordShouldBeSynchronised = res;
  return;
end;

global 
function Boolean PLVcRecordSync(record PLVc PLr,string tagstr)
begin
  Boolean res;
  
  res = true;
  PLVcRecordSync = res;
  return;
end;

global updating procedure CreatePLActivityMn(record RcVc RepSpec)
begin
	record ActVc Actr;
	record UserVc User;
	boolean TrHs,testf,findlock;
	date lastactday;
	time lastacttime;
	record PLHistVc PLHr;
	string 255 fname,docname,location,chloc;
	record INVc INr;
	Record PLVc PLr;
	integer comp,count,pos;
	record ItemStatusVc ISr;
	record PLDefVc PLDefr;
	record BaseCurBlock BCb;
	string 5 curcode;
	string 100 brand,model;
	Record DIVc DIr;
	longint curtick;
	
	DateOfCheckAndPriceChange; // Edit ************************** Ihor Trubachov 21*05*2021
	
	curtick = getcurtick();
	blockload(BCb);
	curcode = BCb.BaseCur1;
	
	comp = currentcompany;
	
	User.Code = "";
	while(loopmain(User,1,true))begin
		logtext(0,"CreatePLActivityMn for " & User.Code);
		testf = true;
		count = 0;
		if(User.Closed>0)then begin testf = false; end;
		if(blank(User.UserLocations))then begin testf = false; end;
		if(User.Code=="SA")then begin testf = true; end;
		
		if(testf)then begin
			count = 0;
			lastactday = stringtodate("1/1/2013");
			lastacttime = stringtotime("0:0");
			Actr.ActType = "CUSRE";
			Actr.StartTime = Currenttime;
			Actr.TransDate =	Currentdate;
			TrHs = true;
			while(loopbackkey("ActType",Actr,3,TrHs))begin
				testf = false;
				if(Actr.ActType!="CUSRE")then begin TrHs=false; end;
				if(TrHs and Actr.MainPersons==User.Code)then begin testf = true; end;
				
				if(testf)then begin
					TrHs = false;
					lastactday = Actr.TransDate;
					lastacttime = Actr.StartTime;
				end;
			end;
			resetloop(Actr);
			
			PLHr.TransDate = currentdate;
			PLHr.TransTime = currenttime;
			TrHs = true;
			logtext(0,"CreatePLActivityMn file " & "PricesHist/Prices Update " & User.Code & "_" & currentcompany & ".xls");
			createfile("PricesHist/Prices Update " & User.Code & "_" & currentcompany & ".xls");
			openexportfile("PricesHist/Prices Update " & User.Code & "_" & currentcompany & ".xls",false);
			setexportcodepage("CP1251");
			startformat(15);
				outstring(0,0,"Товар",false);
				outstring(0,0,"Название",false);
				outstring(0,0,"Модель",false);
				outstring(0,0,"Бренд",false);
				outstring(0,0,"Прайслист",false);
				outstring(0,0,"Серийный номер",false);
				outstring(0,0,"В наличии на складах",false);
				outstring(0,0,"Цена",false);
				outstring(0,0,"Валюта",false);
			endformat;
			
			While(loopbackkey("TransDate",PLHr,2,TrHs))begin
				testf = true;
				if(PLHr.TransDate<lastactday)then begin TrHs = false; testf = false; end;
				if(PLHr.TransDate==lastactday and PLHr.TransTime<lastacttime)then begin TrHs = false; testf = false;  end;
	  		if(PLHr.PLCode!="RRP")then begin testf = false; end;
	  		
	  		if(testf)then begin
	  			findlock = false;
	  			chloc = "";
	  			pos = 0;
	  			ExtractObjWithSeparator(",",User.UserLocations,true,pos,location);
					while(nonblank(location))begin
	  				ISr.Code = PLHr.ArtCode;
	  				ISr.Location = location;
	  				if(readfirstmain(ISr,2,true))then begin
	  					if(ISr.Instock>0)then begin
	  						findlock = true;
	  						chloc = chloc & ISr.Location & ",";
	  					end;
	  				end;
	  				ExtractObjWithSeparator(",",User.UserLocations,true,pos,location);
	  			end;
					if(findlock)then begin
						
						chloc = mid(chloc,0,len(chloc)-1);
						INr.Code = PLHr.ArtCode;
						readfirstmain(INr,1,true);
					
						PLr.PLCode = PLHr.PLCode;
						PLr.ArtCode = PLHr.ArtCode;
						PLr.CustCode = PLHr.CustCode;
						PLr.SerialNr = PLHr.SerialNr;
						readfirstmain(PLr,4,true);
						
						if(nonblank(PLr.CurncyCode))then begin
							curcode = PLr.CurncyCode;
						end else begin
							PLDefr.Code = PLr.PLCode;
							readfirstmain(PLDefr,1,true);
							if(nonblank(PLDefr.CurncyCode))then begin
								curcode = PLDefr.CurncyCode;
							end;
						end;
						startformat(15);
							outstring(0,0,"_" & PLHr.ArtCode,false);
							outstring(0,0,INr.Name,false);
							pos = 0;
							brand = "";
							model = "";
							ExtractObj(INr.DispGroups,pos,brand);
							if(nonblank(brand))then begin
								DIr.Code = brand;
								if(readfirstmain(DIr,1,true))then begin
									brand = DIr.Name;
								end;
							end;
							outstring(0,0,brand,false);
							ExtractObj(INr.DispGroups,pos,model);
							if(nonblank(model))then begin
								DIr.Code = model;
								if(readfirstmain(DIr,1,true))then begin
									model = DIr.Name;
								end;
							end;
							outstring(0,0,model,false);
							
							outstring(0,0,PLr.PLCode,false);
							outstring(0,0,PLr.SerialNr,false);
							outstring(0,0,chloc,false);
							outstring(0,0,PLr.ExVatPrice,false);
							outstring(0,0,curcode,false);
						endformat;
						count = count + 1;
					end;
	  		end;
			end; 
			closefile;
			resetloop(PLHr);
			
			if(count>0)then begin
				recordnew(Actr);
				Actr.MainPersons = User.Code;
				Actr.ActType = "CUSRE";
				Actr.StartTime = CurrentTime;
				Actr.TransDate = CurrentDate;
				Actr.TodoFlag = 1;
				Actr.CalTimeFlag = 1;
				Actr.SymbNr = 3;
				Actr.AlarmType = 1;
				Actr.Comment = "Обновились цены. Список изменений в прикрепленном файле";
				Actr.SerNr = NextSerNr("ActVc",Actr.TransDate,-1,false,"");
				recordinsert(Actr,true);
				if(FileExists("PricesHist/Prices Update " & User.Code & "_" & currentcompany & ".xls"))then begin
					RecordLinkFile("PricesHist/Prices Update " & User.Code & "_" & currentcompany & ".xls",0,Actr,comp);
				end;
			end;
		end;		
	end;

	LogProcTime("CreatePLActivityMn",getcurtick()-curtick); 
return;
end;

global //Edit***************************Sasha2,10:45 01.12.2014 {
updating function LongInt PLVcRecordSave(var record PLVc PLr,record PLVc PL2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record INVc INr,IN2r;
  LongInt foundflag;
  
  if (PLr.ExVatPrice<>0) then begin
  	INr.Code = PLr.ArtCode;
  	if (ReadFirstMain(INr,1,true)) then begin
  		IN2r.SyncFlag = 9999999999;
  		if (ReadLastKey("SyncFlag",IN2r,1,false)) then begin
  			foundflag = IN2r.SyncFlag;
  		end;
  		INr.SyncFlag = foundflag + 1;
  		INr.SavedCount = INr.SavedCount + 1;
  		RECORDSTORE(INr,true);
  	end;
  end;
 
  PLVcRecordSave = res; 
 return;
end; //Edit***************************Sasha2,10:45 01.12.2014 }

global //Edit***************************Sasha2,10:45 01.12.2014 {
updating function LongInt PLVcRecordUpdate(var record PLVc PLr,record PLVc PL2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  record INVc INr,IN2r;
  Boolean itemnotsavedf;
  LongInt foundflag;

  itemnotsavedf = true;
  if (PL2r.ExVatPrice!=PLr.ExVatPrice) then begin
    INr.Code = PLr.ArtCode;
  	if (ReadFirstMain(INr,1,true)) then begin
  		IN2r.SyncFlag = 9999999999;
  		if (ReadLastKey("SyncFlag",IN2r,1,false)) then begin
  			foundflag = IN2r.SyncFlag;
  		end;
  		INr.SyncFlag = foundflag + 1;
  		INr.SavedCount = INr.SavedCount + 1;
  		RECORDSTORE(INr,true);
  		itemnotsavedf = false;
  	end;
  end;
  if (PL2r.ArtCode!=PLr.ArtCode) then begin
    INr.Code = PLr.ArtCode;
  	if (itemnotsavedf and ReadFirstMain(INr,1,true)) then begin
  		IN2r.SyncFlag = 9999999999;
  		if (ReadLastKey("SyncFlag",IN2r,1,false)) then begin
  			foundflag = IN2r.SyncFlag;
  		end;
  		INr.SyncFlag = foundflag + 1;
  		INr.SavedCount = INr.SavedCount + 1;
  		RECORDSTORE(INr,true);
  	end;
  	INr.Code = PL2r.ArtCode;
  	if (ReadFirstMain(INr,1,true)) then begin
  		IN2r.SyncFlag = 9999999999;
  		if (ReadLastKey("SyncFlag",IN2r,1,false)) then begin
  			foundflag = IN2r.SyncFlag;
  		end;
  		INr.SyncFlag = foundflag + 1;
  		INr.SavedCount = INr.SavedCount + 1;
  		RECORDSTORE(INr,true);
  	end;
  end;

  PLVcRecordUpdate = res; 
 return;
end; //Edit***************************Sasha2,10:45 01.12.2014 }