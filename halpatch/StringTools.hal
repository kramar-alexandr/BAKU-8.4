external procedure ExtractObj(string,var Integer,var string);


global
procedure MySortStringArray(var array string astr,integer cnt)
begin
	integer i,j;
	string 100 tmp1,tmp2;
	
	if(cnt>1)then begin
		for(j=0;j<cnt;j=j+1)begin
			for(i=0;i<cnt-1;i=i+1)begin
				if(uppercase(astr[i])>uppercase(astr[i+1]))then begin
					tmp1 = astr[i];
					tmp2 = astr[i+1];
					astr[i] = tmp2;
					astr[i+1] = tmp1;
				end;
			end;
		end;
	end;
		

return;
end;


global
function Boolean FindSubString( string Search, string Source )		//Edit----------------------Dima  02.03.2015
begin
	integer i,length,lengthSource,diff;
	string 150 temp,str,inStr;
	boolean res;
	
	res=false;
	length=len(Search);
	lengthSource=len(Source);
	diff=lengthSource-length;

	str=UpperCase(Search);   
	inStr=UpperCase(Source);		
	if (diff<0) then begin
	goto	breakFunction;
	end;
	
	for(i=0;i<=diff;i=i+1) begin
	 temp=mid(inStr,i,length);
	 if(temp==str) then begin
	 	res = true;
	 	goto breakFunction;
	 end;	
	end;

	breakFunction:; // goto
	FindSubString=res;
return;  
end;


//pos - from 0 to length-1. This is  position of substring
global 
procedure  ReplaceSubstringInString(var string source, string put, integer pos)		//Edit----------------------Dima  27.02.2015
begin
	string 255 result;
	integer lenSubStr;
	
	if ( Len(put) + pos <= Len(source)) then begin
		lenSubStr = Len(put);
	end else begin
		lenSubStr = Len(source)-pos;
	end;
	
	result = Left(source,pos) & Left(put,lenSubStr) & Right(source, Len(source)-pos-lenSubStr);
	source = result;
  
end;



global
function string 255 InsertSubStringInString(string source, string put, integer pos) //Edit----------------------Dima  02.03.2015
begin
	string 255 result;
	
	result = Left(source,pos) &  put & Right(source,Len(source)-pos);
  InsertSubStringInString = result;
return;
end;



global
function Integer FindFirstPosStringInString( string Search, string Source )//Edit----------------------Dima  20.02.2015
begin
	integer i,length,lengthSource,diff;
	string 150 temp,str,inStr;
	integer res;
	
	res=-1;
	length=len(Search);
	lengthSource=len(Source);
	diff=lengthSource-length;

	str=UpperCase(Search);   
	inStr=UpperCase(Source);		
	if (diff<0) then begin
	goto	LbreakFirstPos;
	end;
	
	for(i=0;i<=diff;i=i+1) begin
	 temp=mid(inStr,i,length);
	 if(temp==str) then begin
	 	res = i;
	 	goto LbreakFirstPos;
	 end;	
	end;

	LbreakFirstPos:; 
	FindFirstPosStringInString=res;
return;  
end;

global
function string 255 ChangeCaseStr(string source, integer format) //Edit-------------------Vitalii 11:34 14.09.2016
begin
/*
format =
1 - UpperCase
2 - LowerCase
3 - Like sentence
4 - First letter is big in all words
*/
	string 255 res;
  integer i;
  
  switch (format) begin
    case 1:
      res = UpperCase(source);
    case 2:
      res = LowerCase(source);
    case 3:
      res = UpperCase(left(source,1)) & LowerCase(right(source,(len(source) - 1)));
    case 4:
      for(i=0;i<=len(source);i=i+1) begin
        if (i==0) or
           (Mid(source,i-1,1) == " ") or (Mid(source,i-1,1) == ",") or
           (Mid(source,i-1,1) == "_") or (Mid(source,i-1,1) == ".") or
           (Mid(source,i-1,1) == ";") or (Mid(source,i-1,1) == ":") or
           (Mid(source,i-1,1) == "(") or (Mid(source,i-1,1) == ")") or
           (Mid(source,i-1,1) == "[") or (Mid(source,i-1,1) == "]") or
           (Mid(source,i-1,1) == "{") or (Mid(source,i-1,1) == "}") or
           (Mid(source,i-1,1) == "/") or (Mid(source,i-1,1) == "\\") or
           (Mid(source,i-1,1) == "'") or (Mid(source,i-1,1) == "\"") or
           (Mid(source,i-1,1) == "-") or (Mid(source,i-1,1) == "+") or
           (Mid(source,i-1,1) == "=") or (Mid(source,i-1,1) == "?") or
           (Mid(source,i-1,1) == "&") or (Mid(source,i-1,1) == "!") or
           (Mid(source,i-1,1) == "@") or (Mid(source,i-1,1) == "#") or
           (Mid(source,i-1,1) == "$") or (Mid(source,i-1,1) == "%") or
           (Mid(source,i-1,1) == "^") or (Mid(source,i-1,1) == "*") then begin
          res = res & UpperCase(Mid(source,i,1));
        end else begin
          res = res & LowerCase(Mid(source,i,1));
        end;
      end;
	end;
	ChangeCaseStr = res;
return;
end;

global function string 255 NormalizeSetString(string instr)// Edit ************************** Tuesday, 31 October 2017 17:47:41
begin
	string 255 res,tstr;
	integer pos,i,acnt;
	vector boolean objf;
	array string 255 objsrt;
	
	if(nonblank(instr))then begin
		pos = 0;
		ExtractObj(instr,pos,tstr);
		while(nonblank(tstr))begin
			if(nonblank(tstr))then begin
				if(objf[tstr]==false)then begin
					objf[tstr] = true;
					objsrt[acnt] = tstr;
					acnt = acnt + 1;
				end;
			end;
			ExtractObj(instr,pos,tstr);
		end;
	end;
	
	if(acnt>0)then begin
		MySortStringArray(objsrt,acnt);
		For(i=0;i<acnt;i=i+1) begin
			if(i>0)then begin
	  		res = res & "," & objsrt[i];
	  	end else begin
	  		res = objsrt[i];
	  	end;
		end; 
	end;
	
	NormalizeSetString = res;
	
return;
end;