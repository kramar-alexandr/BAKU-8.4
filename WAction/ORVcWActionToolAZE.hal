external function string 255 StrReplace(string,string,string);

SetLangMode(LangAzerbaijani,"AZE",0);

function string 255 ThreeDigitsToWord(longint in, vector string w)
begin
  string 255 res;
  roundmode rnd;
  
  if (in!=0) then begin
    rnd = DefaultCurRoundoff;
    rnd.decimals = 0;
    rnd.step = kRoundingStepNone;
    rnd.mode = kRoundingModeHalfDown;
    if (mod(in,10)>0) then begin
      res = " " & w["" & mod(in,10)];
    end;
    if (mod(round(in/10,rnd),10)>0) then begin
      res = " " & w[mod(round(in/10,rnd),10) & "0"] & res;
    end;
    res = w[mod(round(in/100,rnd),10) & "00"] & res;
  end;
  ThreeDigitsToWord = res;
  return;
end;

function string 255 ValToWordAz(val in, string currency)
begin
  vector string 50 azdigits;
  longint inInt,inFract,tmp,tmp2,tmp3;
  string 255 out,tdtw;
  roundmode rnd;
  
  rnd = DefaultCurRoundoff;
  rnd.decimals = 0;
  rnd.step = kRoundingStepNone;
  rnd.mode = kRoundingModeTruncate;
  
  azdigits["0"]       = "sıfır";
  azdigits["1"]       = "bir";
  azdigits["2"]       = "iki";
  azdigits["3"]       = "üç";
  azdigits["4"]       = "dörd";
  azdigits["5"]       = "beş";
  azdigits["6"]       = "altı";
  azdigits["7"]       = "yeddi";
  azdigits["8"]       = "səkkiz";
  azdigits["9"]       = "doqquz";
  azdigits["10"]      = "on";
  azdigits["20"]      = "iyirmi";
  azdigits["30"]      = "otuz";
  azdigits["40"]      = "qırx";
  azdigits["50"]      = "əlli";
  azdigits["60"]      = "altmış";
  azdigits["70"]      = "yetmiş";
  azdigits["80"]      = "səksən";
  azdigits["90"]      = "doqsan";
  azdigits["100"]     = "yüz";
  azdigits["200"]     = azdigits["2"] & " " & azdigits["100"];
  azdigits["300"]     = azdigits["3"] & " " & azdigits["100"];
  azdigits["400"]     = azdigits["4"] & " " & azdigits["100"];
  azdigits["500"]     = azdigits["5"] & " " & azdigits["100"];
  azdigits["600"]     = azdigits["6"] & " " & azdigits["100"];
  azdigits["700"]     = azdigits["7"] & " " & azdigits["100"];
  azdigits["800"]     = azdigits["8"] & " " & azdigits["100"];
  azdigits["900"]     = azdigits["9"] & " " & azdigits["100"];
  azdigits["1000"]    = "min";
  azdigits["1000000"] = "milyon";
  azdigits["and"]     = "və";
  
  inInt = round(in,rnd);
  rnd.mode = kRoundingModeHalfUp;
  inFract = round((in - inInt)*100,rnd);
  
  tmp = mod(inInt,1000);
  out = ThreeDigitsToWord(tmp,azdigits);
  
  if (inInt>999) then begin
    tmp2 = mod((inInt - tmp)/1000,1000);
    tdtw = "";
    tdtw = ThreeDigitsToWord(tmp2,azdigits);
    if nonblank(tdtw) then begin
      out = tdtw & " " & azdigits["1000"] & " " & out;
    end;
    out = StrReplace(out,azdigits["1"] & " " & azdigits["1000"],azdigits["1000"]);
  end;
  if (inInt>999999) then begin
    tmp3 = mod((inInt - tmp - tmp2*1000)/1000000,1000);
    tdtw = "";
    tdtw = ThreeDigitsToWord(tmp3,azdigits);
    if nonblank(tdtw) then begin
      out = tdtw & " " & azdigits["1000000"] & " " & out;
    end;
  end;
  
  if (inFract>0) then begin
    out = out & " " & azdigits["and"] & " " & ThreeDigitsToWord(inFract,azdigits);
  end;
  
  out = StrReplace(out,"  "," ");
  //out = StrReplace(out,"bir milyon","milyon");
  //out = StrReplace(out,"milyon min","milyon");
  
  if (left(out,1)==" ") then begin
    out = right(out,len(out)-1);
  end;
  if (right(out,1)==" ") then begin
    out = left(out,len(out)-1);
  end;
  if nonblank(out) then begin
    switch(currency) begin
      case "AZN": out = out & " manat";
      case "EUR": out = out & " avro";
      case "USD": out = out & " dollar";
    end;
  end;
  
  ValToWordAz = out;
  return;
end;

global
procedure IDPasteDescs(var record ORVc ORr)
begin
  ORr.IDPRCostDesc = ValToWordAz(ORr.Sum4,ORr.CurncyCode);
  ORr.IDPrepDesc = ValToWordAz(ORr.IDPrepay,ORr.CurncyCode);
  ORr.IDBalPayDesc = ValToWordAz(ORr.IDBalPay,ORr.CurncyCode);
end;