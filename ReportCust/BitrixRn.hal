
SetLangMode(LangRussian,"RUS",0);


global
procedure ClientsRn(record RcVc RepSpec)  // Edit ****************************** Ihor Trubachov 14*09*2021 
begin

	record IVVc IVr;
	record ARVc ARr;
	record PUVc PUr;
	record POVc POr;
	record ORVc ORr;
	row ORVc ORrw;
	record RLinkVc RLr;
	record RetVc Retr;
	row RetVc Retrw;
	Boolean TrHs,TrHs1,testf,testf1, chClose;
	Integer i,j,k, cnrw;
	vector val vSumOrders;
	vector Date vSt_End_Order;
	vector String 255 vFullName, vEmail;
	array string 255 tags;
	val RetSum, Ret;

	StartReportnoheaderjob("Отчет по клиентам Е-Коммерс");


	StartFormat(15);
		OutString(30,0,"ФИО",false);
		OutString(40,0,"Дата первого заказа",false);
		OutString(50,0,"Дата последнего заказа",false);
		OutString(60,0,"Кол-во заказов",false);
		OutString(70,0,"Сумма оплаченных заказов",false);
		OutString(80,0,"Себестоимость заказа",false);
		OutString(90,0,"Email",false);
		OutString(100,0,"Телефон",false);
	EndFormat;
	Black_Divider(0,1);
	
	TrHs = true;
	while(LoopMain(ORr,1,TrHs))begin
		testf = true;
		testf1 = true;
		RetSum = blankval;
		chClose = false;
	
		if(nonblank(ORr.Phone))then begin
		
			vFullName[ORr.Phone] = ORr.Addr0;
			
			vEmail[ORr.Phone] = ORr.eMail;
			vSumOrders[ORr.Phone & "Qty"] = vSumOrders[ORr.Phone & "Qty"] + 1;
			
			if(ORr.Closed!=0)then begin
				chClose = true;
			end;
			
			if(!chClose)then begin
				
				cnrw = MatRowCnt(ORr);
				for(i=0;i<cnrw;i=i+1)begin
					MatRowGet(ORr,i,ORrw);
					vSumOrders[ORr.Phone & "cntGet"] = vSumOrders[ORr.Phone & "cntGet"] + ORrw.Quant;
					
					
					TrHs1 = true;
					Ret = blankval;
					Retr.OrdNr = ORr.SerNr;
					while(LoopKey("OrdNr",Retr,1,TrHs1))begin
						if(Retr.OrdNr!=ORr.SerNr)then begin TrHs1 = false; end;
						
						if(TrHs1)then begin
							for(k=0;k<MatRowCnt(Retr);k=k+1)begin
								MatRowGet(Retr,k,Retrw);
								
							
								if(Retrw.ArtCode==ORrw.ArtCode)then begin
									vSumOrders[ORr.Phone & "cntRet"] = vSumOrders[ORr.Phone & "cntRet"] + Retrw.Quant;
									
									Ret = Ret + Retrw.BBCostPrice * Retrw.Quant;
									//RetSum = RetSum+Ret;
									vSumOrders[ORr.Phone & "RetSum"] = vSumOrders[ORr.Phone & "RetSum"] + (Retrw.BBCostPrice * Retrw.Quant); // RetSum
									TrHs1 = false;
								end;
							end;
						end;
					end;
					
					if(blank(ORrw.ECReservStock))then begin
						if(testf1)then begin
							if(ReadRecordLink(ORr,1,POr,RLr))then begin
								if(ReadRecordLink(POr,1,PUr,RLr) and PUr.OKFlag==1)then begin
									vSumOrders[ORr.Phone & "Cst"] =	vSumOrders[ORr.Phone & "Cst"] +	PUr.SumCostPrice;
									testf1 = false;
								end;
							end;
						end;
					end else begin
						if(testf)then begin
							j=1;
							while(ReadRecordLink(ORr,j,PUr,RLr) and PUr.OKFlag==1)begin
								vSumOrders[ORr.Phone & "Cst"] =	vSumOrders[ORr.Phone & "Cst"] +	PUr.SumCostPrice;
								j=j+1;
								testf = false;
							end;
						end;
						
					end;
					
				end;
				
			end else begin
				
				vSumOrders[ORr.Phone & "Cst"] = 0;
				vSumOrders[ORr.Phone & "RetSum"] = 0;
				vSumOrders[ORr.Phone & "cntRet"] = 0;
				vSumOrders[ORr.Phone & "cntGet"] = 0;
				
			end;
			
			
			if(blankDate(vSt_End_Order[ORr.Phone & "Str"])) then begin
				vSt_End_Order[ORr.Phone & "Str"] = ORr.OrdDate;
			end;
			if(blankDate(vSt_End_Order[ORr.Phone & "End"])) then begin
				vSt_End_Order[ORr.Phone & "End"] = ORr.OrdDate;
			end;
		
			if(vSt_End_Order[ORr.Phone & "Str"] > ORr.OrdDate) then begin
				vSt_End_Order[ORr.Phone & "Str"] = ORr.OrdDate;
			end;
			if(vSt_End_Order[ORr.Phone & "End"] < ORr.OrdDate) then begin
				vSt_End_Order[ORr.Phone & "End"] = ORr.OrdDate;
			end;
			
			if(!chClose)then begin
				if(ReadRecordLink(ORr,1,IVr,RLr))then begin					
					if(IVr.OKFlag==1)then begin	// Edit ****************************** Ihor Trubachov 13*10*2021 
						ARr.InvoiceNr = IVr.SerNr;
						if(ReadFirstMain(ARr,1,true))then begin
							vSumOrders[ORr.Phone & "Sum"] = vSumOrders[ORr.Phone & "Sum"] + (IVr.Sum4 - ARr.RVal);
						end else begin
							vSumOrders[ORr.Phone & "Sum"] = vSumOrders[ORr.Phone & "Sum"] + IVr.Sum4;
						end;
					end else begin
						vSumOrders[ORr.Phone & "Sum"] = vSumOrders[ORr.Phone & "Sum"] + 0;
					end;
					
					
				end;
			end else begin	
				vSumOrders[ORr.Phone & "Sum"] = 0;
			end;
			
			
		end;
		
	end;
	

	

	GetVectorTags(vFullName,tags);
	SortStringArray(tags);
	for(i=0;i<tags.length;i=i+1) begin
		StartFormat(15);
		OutString(30,0,vFullName[tags[i]],false);
		OutString(40,0,vSt_End_Order[tags[i] & "Str"],false);
		OutString(50,0,vSt_End_Order[tags[i] & "End"],false);
		OutString(60,0,vSumOrders[tags[i] & "Qty"],false);
		 
		if((vSumOrders[tags[i] & "Sum"] - vSumOrders[tags[i] & "RetSum"])==0 and vSumOrders[tags[i] & "cntGet"] > vSumOrders[tags[i] & "cntRet"])then begin
			OutString(70,0,"",false);
		end else begin
			if(vSumOrders[tags[i] & "cntGet"]<=vSumOrders[tags[i] & "cntRet"])then begin
				OutString(70,0,0,false);
			end else begin
				OutString(70,0,vSumOrders[tags[i] & "Sum"] - vSumOrders[tags[i] & "RetSum"],false);
			end;
			
		end;
		
		//OutString(80,0,vSumOrders[tags[i] & "Cst"],false);
		
		
		if((vSumOrders[tags[i] & "Cst"] - vSumOrders[tags[i] & "RetSum"])==0 and vSumOrders[tags[i] & "cntGet"] > vSumOrders[tags[i] & "cntRet"])then begin
			OutString(80,0,"",false);
		end else begin
			if(vSumOrders[tags[i] & "cntGet"]<=vSumOrders[tags[i] & "cntRet"])then begin
				OutString(80,0,0,false);
			end else begin
				OutString(80,0,vSumOrders[tags[i] & "Cst"] - vSumOrders[tags[i] & "RetSum"],false);
			end;
			
		end;
		
		
		
		OutString(90,0,vEmail[tags[i]],false);
		OutString(100,0,tags[i],false);
		EndFormat;
	end;


	ClearVector(vFullName);
	ClearVector(vSt_End_Order);
	ClearVector(vSumOrders);
	ClearVector(vEmail);
	ClearArray(tags);
	
	EndJob;

return;
end;





global
procedure SalesRn(record RcVc RepSpec)  // Edit ****************************** Ihor Trubachov 14*09*2021 
begin

	record ORVc ORr;
	row ORVc ORrw;
	record POVc POr;
	record PUVc PUr;
	record IVVc IVr;
	record ARVc ARr;
	record RLinkVc RLr;
	record RetVc Retr;
	row RetVc Retrw;
	Boolean TrHs,TrHs1,testf,testf1,testf2, chClose;
	Integer i,j,cnrw;
	val SumOrders, RValPaid, Ret, cntRet, cntGet;
	date sd,td;

	StartReportnoheaderjob("Отчет по продажам");

	StartFormat(15);
		OutString(30,0,"ФИО",false);
		OutString(40,0,"Дата",false);
		OutString(50,0,"Номер Заказа",false);
		OutString(60,0,"Сумма заказа",false);
		OutString(70,0,"Себестоимость",false);
		OutString(80,0,"Оплаченная Сумма",false);
		OutString(90,0,"Email",false);
		OutString(100,0,"Телефон",false);
		OutString(110,0,"Sorse of traffic",false);
	EndFormat;
	Black_Divider(0,1);
	
	sd = RepSpec.sStartDate;
	td = RepSpec.sEndDate;

	TrHs = true;
	while(LoopMain(ORr,1,TrHs))begin
		testf = true;
		testf1 = true;
		testf2 = true;
		SumOrders = blankval;
		RValPaid = blankval;
		chClose = false;
		cntRet = 0;
		cntGet = 0;
		
		if (DateInRange(ORr.OrdDate,sd,td)==false) then begin testf = false; end;
		
		if(testf)then begin
			StartFormat(15);
				OutString(30,0,ORr.Addr0,false);
				OutString(40,0,ORr.OrdDate,false);				
				OutString(50,0,ORr.SerNr,false);
				
				OutString(60,0,ORr.Sum4,false);
				
				cnrw = MatRowCnt(ORr);
				for(i=0;i<cnrw;i=i+1)begin
					MatRowGet(ORr,i,ORrw);
					cntGet = cntGet + Retrw.Quant;
					
					/////
					Retr.OrdNr = ORr.SerNr;
					TrHs1 = true;
					Ret = blankval;
					while(LoopKey("OrdNr",Retr,1,TrHs1))begin
						if(Retr.OrdNr!=ORr.SerNr)then begin TrHs1 = false; end;
						
						if(TrHs1)then begin
							for(j=0;j<MatRowCnt(Retr);j=j+1)begin
								MatRowGet(Retr,j,Retrw);
								if(Retrw.ArtCode==ORrw.ArtCode)then begin
									cntRet = cntRet + Retrw.Quant;
									Ret = Ret + Retrw.BBCostPrice * Retrw.Quant;
									TrHs1 = false;
								end;
							end;
						end;
					end;
					/////
					
					if(blank(ORrw.ECReservStock))then begin
						if(testf1)then begin
							if(ReadRecordLink(ORr,1,POr,RLr))then begin
								if(ReadRecordLink(POr,1,PUr,RLr))then begin
									SumOrders =	SumOrders +	PUr.SumCostPrice;
									testf1 = false;
								end;
							end;
						end;
					end else begin
						if(testf2)then begin
							j=1;
							while(ReadRecordLink(ORr,j,PUr,RLr))begin
								SumOrders =	SumOrders +	PUr.SumCostPrice;
								j=j+1;
								testf2 = false;
							end;
						end;	
					end;
				end;
				
				
				if(ORr.Closed!=0)then begin
					chClose=true;
				end;
				
				if(!chClose)then begin
					OutString(70,0,0,false);
				end else begin
					if((SumOrders - Ret)==0 and cntGet > cntRet)then begin
						OutString(70,0,"",false);
					end else begin
						if(cntGet == cntRet)then begin
							OutString(70,0,0,false);
						end else begin
							OutString(70,0,SumOrders - Ret,false);
						end;
					end;
				end;
				
				
				if(ReadRecordLink(ORr,1,IVr,RLr))then begin // Edit ****************************** Ihor Trubachov 13*10*2021 
				
				 if(IVr.OKFlag==1)then begin
						ARr.InvoiceNr = IVr.SerNr;
						if(ReadFirstMain(ARr,1,true))then begin
							RValPaid = IVr.Sum4 - ARr.RVal;  //////
						end else begin
							RValPaid = IVr.Sum4;
						end;
					end else begin
						RValPaid = 0;
					end;
					
				end;
				
				
				if(!chClose)then begin
					OutString(80,0,0,false);
				end else begin
					if((RValPaid - Ret)==0 and cntGet > cntRet)then begin
						OutString(80,0,"",false);
					end else begin
						if(cntGet == cntRet)then begin
							OutString(80,0,0,false);
						end else begin
							OutString(80,0,RValPaid - Ret,false);
						end;
					end;
				end;
				
				OutString(90,0,ORr.eMail,false);
				OutString(100,0,ORr.Phone,false);
			EndFormat;
		end;

	end;

	
	EndJob;
	

return;
end;




global
procedure ExtSalesRn(record RcVc RepSpec)  // Edit ****************************** Ihor Trubachov 16*09*2021 
begin
	record ORVc ORr;
	row ORVc ORrw;
	record POVc POr;
	record PUVc PUr;
	row PUVc PUrw;
	record INVc INr;
	record IVVc IVr, IV2r;
	row IVVc IVrw, IV2rw;
	record VIVc VIr;
	record ARVc ARr;
	record APVc APr;
	record RLinkVc RLr;
	record RetVc Retr;
	row RetVc Retrw;
	record GlobalItemVc GlobalItemr; 
	record BtrxFirstLevelCatVc BtrxFirstLevelCatr;
	Boolean TrHs,testf,testf1,testf2,TrHs1;
	Integer i,j,k,cnrw;
	val Ret, RValPaid,oblRVal, SumOrders;
	date sd,td;
	String 255 strObj;
	//
	val SumCredIv, SumSH, SumIv;
	Boolean chSH;
	record SHVc SHr;
	row SHVc SHrw;

	StartReportnoheaderjob("Расширенный отчет по продажам");

	StartFormat(15);
		OutString(30,0,"ФИО",false); 														// +
		OutString(40,0,"Дата",false);                           // +
		OutString(50,0,"Номер Заказа",false);                   // +
		OutString(60,0,"Артикул",false);                        // +
		OutString(70,0,"Название Товара",false);                // +
		OutString(80,0,"Поставщик",false);                      // +
		OutString(90,0,"Категория",false);                      // +
		OutString(100,0,"Сумма",false);                         // +
		OutString(110,0,"Возврат",false);                       // + // RetVc RetDClass: 
		OutString(120,0,"Скидка",false);                        // +
		OutString(130,0,"Карта Italdizain/промо код",false);    // +
		OutString(140,0,"Долг поставщику",false);               // +
		OutString(150,0,"Оплачено",false);                      // +
		OutString(160,0,"Прибыль",false);                       // +
		OutString(170,0,"Email",false);                         // +
		OutString(180,0,"Телефон",false);                       // +
	EndFormat;
	Black_Divider(0,1);
	
	

	sd = RepSpec.sStartDate;
	td = RepSpec.sEndDate;

	TrHs = true;
	while(LoopMain(ORr,1,TrHs))begin
	  k = 1; 
		testf = true;
		testf1 = true;
		testf2 = true;
		Ret = blankval;
		SumOrders  = blankval;

		
		
		///
    chSH = false;
		///
		
		if (DateInRange(ORr.OrdDate,sd,td)==false) then begin testf = false; end;
		
		if(testf)then begin
		
			// while(ReadRecordLink(ORr,k,IVr,RLr))begin

			if(ReadRecordLink(ORr,1,SHr,RLr))then begin
				chSH = true;
			end;
			
			
			cnrw = MatRowCnt(ORr);
			for(i=0;i<cnrw;i=i+1)begin
				MatRowGet(ORr,i,ORrw);
				
				RValPaid = blankval;
				oblRVal = blankval;
				
				SumCredIv = 0;
				SumIv = 0;
				SumSH = 0;
				
				StartFormat(15);
					OutString(30,0,ORr.Addr0,false);
					OutString(40,0,ORr.OrdDate,false);
					OutString(50,0,ORr.SerNr,false);
					
				
					INr.Code = ORrw.ArtCode;
					if(ReadFirstMain(INr,1,true))then begin
						GlobalItemr.Code = INr.Code;
						if(ReadFirstMain(GlobalItemr,1,true))then begin
							OutString(60,0,GlobalItemr.HansaCode,false); 
						end;
						
						testf1 = true;
					end else begin
						OutString(60,0,"",false); 
						testf1 = false;
					end;
					
					OutString(70,0,ORrw.Spec,false);   

					strObj = "";
					if(ReadRecordLink(ORr,1,POr,RLr))then begin
						strObj = POr.VECode;
					end else begin
						strObj = "";
					end;          
					OutString(80,0,strObj,false);   
					
					strObj = "";
					if(testf1)then begin
						GlobalItemr.Code = INr.Code;
						if(ReadFirstMain(GlobalItemr,1,true))then begin
							BtrxFirstLevelCatr.Code = GlobalItemr.BTRxFirstLevCat;
							if(ReadFirstMain(BtrxFirstLevelCatr,1,true))then begin
								strObj = BtrxFirstLevelCatr.NameRUS;
							end;
						end;
					end;
					OutString(90,0,strObj,false);
					OutString(100,0,ORrw.Sum,false);     

					Retr.OrdNr = ORr.SerNr;
					TrHs1 = true;
					Ret = blankval;
					while(LoopKey("OrdNr",Retr,1,TrHs1))begin
						if(Retr.OrdNr!=ORr.SerNr)then begin TrHs1 = false; end;
						
						if(TrHs1)then begin
							for(j=0;j<MatRowCnt(Retr);j=j+1)begin
								MatRowGet(Retr,j,Retrw);
								if(Retrw.ArtCode==ORrw.ArtCode)then begin
									Ret = Ret + Retrw.BBCostPrice * Retrw.Quant;
									TrHs1 = false;
								end;
							end;
						end;
					end;
					OutString(110,0,Ret,false); 
					if(ORrw.vRebate!=blankval)then begin
						OutString(120,0,ORrw.vRebate & "%",false);     
					end else begin
						OutString(120,0,ORrw.vRebate,false);
					end;
					OutString(130,0,ORr.ECLoyaltyCard,false); 
					
					
					
					// if(ReadRecordLink(ORr,1,VIr,RLr))then begin
						// APr.InvoiceNr = VIr.SerNr;
						// if(ReadFirstMain(ARr,1,true))then begin
							// oblRVal = APr.RVal;
						// end;
					// end;			
					//OutString(140,0,oblRVal,false);    

					RValPaid = blankval;
					if(ReadRecordLink(ORr,1,IVr,RLr))then begin // Edit ****************************** Ihor Trubachov 13*10*2021 
				
					 if(IVr.OKFlag==1)then begin
							ARr.InvoiceNr = IVr.SerNr;
							if(ReadFirstMain(ARr,1,true))then begin
								RValPaid = IVr.Sum4 - ARr.RVal;
							end else begin
								RValPaid = IVr.Sum4;
							end;
						end else begin
							RValPaid = 0;
						end;
						
					end;
					OutString(140,0,ORrw.Sum-RValPaid,false);    
					OutString(150,0,RValPaid,false);  

					testf1 = true;
					if(blank(ORrw.ECReservStock))then begin
						if(ReadRecordLink(ORr,1,POr,RLr))then begin
							if(ReadRecordLink(POr,1,PUr,RLr))then begin
								for(j=0;j<MatRowCnt(PUr);j=j+1)begin
									MatRowGet(PUr,j,PUrw);
									if(PUrw.ArtCode==ORrw.ArtCode)then begin
										SumOrders =	PUrw.Sum;
									end;
								end;
							end;
						end;
					end else begin
						k=1;
						while(ReadRecordLink(ORr,k,PUr,RLr))begin
							if(testf1)then begin
								for(j=0;j<MatRowCnt(PUr);j=j+1)begin
									MatRowGet(PUr,j,PUrw);
									if(PUrw.ArtCode==ORrw.ArtCode)then begin
										SumOrders =	PUrw.Sum;
										testf1 = false;
									end;
								end;
							end;
							k=k+1;
						end;
					end;
					
					if(chSH)then begin
						for(j=0;j<MatRowCnt(SHr);j=j+1)begin
							MatRowGet(SHr,j,SHrw);
							if(ORrw.ArtCode == SHrw.ArtCode) then begin
								SumSH = SumSH + SHrw.FIFORowVal;
							end;
						end;
					end;
					
					k=1;
					while(ReadRecordLink(ORr,k,IVr,RLr))begin
						if(IVr.PayDeal == "CN")then begin
							for(j=0;j<MatRowCnt(IVr);j=j+1)begin
								MatRowGet(IVr,j,IVrw);
								if(ORrw.ArtCode == IVrw.ArtCode) then begin
									SumCredIv = SumCredIv + IVrw.Sum;
								end;
							end;
						end else begin
							for(j=0;j<MatRowCnt(IVr);j=j+1)begin
								MatRowGet(IVr,j,IVrw);
								if(ORrw.ArtCode == IVrw.ArtCode) then begin
									SumIv = SumIv + IVrw.Sum;
								end;
							end;
						end;
						
						k=k+1;
					end;
					
					//OutString(160,0,RValPaid - SumOrders,false); // ORrw.Sum - SumOrders
					OutString(160,0,(SumIv - SumCredIv - SumSH + Ret),false); 
					
					OutString(170,0,ORr.eMail,false);
					OutString(180,0,ORr.Phone,false);
				EndFormat; 
			end;
		end;

	end;



	EndJob;
	

return;
end;


//Edit ******************************Vas-P	15/09/2021
global
procedure GeneralRepRn(record RcVc RepSpec)  
begin
	record RcVc RepSpec1;
	integer i,j,m,month,year;
	date sd,td,tmpdate,enddate;
	string 50 monthname;
	record ORVc ORr;
	record SHVc SHr;
	row ORVc ORrw;
	boolean TrHs,TrHs2,testf,completed;
	array string 255 orquant,shquant;
	val orsum,shsum,idquant,otherquant,wholesum,perc;
	

	if(RepSpec.sEndDate>CurrentDate)then begin
		RepSpec.sEndDate = CurrentDate;
	end;
	sd = RepSpec.sStartDate;
	td = RepSpec.sEndDate;
	
	StartReportNoHeaderJob("GeneralReport");
		Startformat(15);
			OutString(0,0,"Год",false);
			OutString(20,0,"Месяц",false);
			OutString(55,0,"Всего заказов",false);
			OutString(105,0,"Сумма всего",false);
			OutString(155,0,"Куплено заказов",false);
			OutString(200,0,"Куплено сумма",false);
			OutString(240,0,"Купленые товары Italdizain",false);
			OutString(280,0,"Купленые товары Сторонние поставищики",false);
			OutString(335,0,"% сторонних поставщиков",false);
			OutString(430,0,"Средний чек",false);
		EndFormat;
		Black_Divider(0,1);
		tmpdate = sd;
		enddate = td;
		while(tmpdate<=enddate)begin
			month = GetMonth(tmpdate);
			year = GetYear(tmpdate);
			switch (month) begin
				case 1: monthname = "Январь";
				case 2: monthname = "Февраль";
				case 3: monthname = "Март";
				case 4: monthname = "Апрель";
				case 5: monthname = "Май";
				case 6: monthname = "Июнь";
				case 7: monthname = "Июль";
				case 8: monthname = "Август";
				case 9: monthname = "Сентябрь";
				case 10: monthname = "Октябрь";
				case 11: monthname = "Ноябрь";
				case 12: monthname = "Декабрь";
			end;
			Startformat(15);
				OutString(0,0,year,false);
				OutString(20,0,monthname,false);
			
				cleararray(orquant);
				cleararray(shquant);
				orsum = 0;
				shsum = 0;
				idquant = 0;
				otherquant = 0;
				
				TrHs = true;
				ORr.SerNr = "";
				ORr.OrdDate = tmpdate;
				while(loopkey("OrdDate",ORr,1,TrHs))begin
					if(ORr.OrdDate>enddate)then begin TrHs = false; end;
					if(month==GetMonth(ORr.OrdDate))then begin
						orquant[orquant.length] = ORr.SerNr;
						orsum = orsum + ORr.Sum4;
						
						SHr.OrderNr = ORr.SerNr;
						if (ReadFirstKey("OrderKey",SHr,1,true)) then begin
							if (SHr.OKFlag==1) then begin
								shquant[shquant.length] = SHr.SerNr;
								
								for (i=0;i<matrowcnt(ORr);i=i+1) begin
									matrowget(ORr,i,ORrw);
									
									shsum = shsum + ORrw.Shipd2 * ORrw.Price * ORrw.vRebate; // Edit ********************** Ihor Trubachov 25*10*2021
									if(ORrw.ECReservComp>0 and nonblank(ORrw.ECReservStock))then begin
										idquant = idquant + ORrw.Shipd2;										
									end else begin
										otherquant = otherquant + ORrw.Shipd2;
									end;									
								end;
							end;
						end;
					end;
				end;
				ResetLoop(ORr);
				
				wholesum = 0;
				
				OutString(60,0,orquant.length,false);
				OutString(105,0,orsum,false);
				OutString(155,0,shquant.length,false);
				OutString(200,0,shsum,false);
				OutString(240,0,idquant,false);
				OutString(280,0,otherquant,false);
				
				wholesum = idquant + otherquant;
				perc = (otherquant/wholesum) * 100;
				
				OutString(335,0,perc & "%",false);
				OutString(430,0,shsum / shquant.length,false);
			EndFormat;
				tmpdate = AddMonth(tmpdate,1);
		end;
	EndJob;
return;
end;