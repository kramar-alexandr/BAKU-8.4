//server-only
external procedure LogProcTime(string,longint);
external procedure ExtractObj(string,var Integer,var string);

SetLangMode(LangRussian,"RUS",0);

global
procedure ReserveRn(record RcVc RepSpec)
begin
  record INVc INr;
  record ORVc ORr;
	row ORVc ORrw;
	boolean TrHs,testf;
	vector string 255 orders,custCode,custName,OrdDate,reservDate;
	vector integer quantShip,rownr;
  longint curtick;
	integer rowcnt,i;
	array string 255 tags;
	integer itemall,pos;
	string 255 orderNr;
	
	ORr.Reserved = 1;
	TrHs = true;
	while(LoopKey("Reserved",ORr,1,TrHs)) begin
		if(ORr.Reserved<1) then begin TrHs = false; end;
		if(TrHs) then begin
			rowcnt = MatRowCnt(ORr);
			for(i=0;i<rowcnt;i=i+1) begin
				testf = true;
				matrowget(ORr,i,ORrw);
				if(nonblank(RepSpec.f1) and RepSpec.f1!=ORrw.ArtCode) then begin testf = false; end;
				if(ORrw.Quant>0 and testf and ORrw.Quant>ORrw.Shipd2) then begin
					if(blank(orders[ORrw.ArtCode])) then begin
						orders[ORrw.ArtCode] = ORr.SerNr;
					end else begin
						orders[ORrw.ArtCode] = orders[ORrw.ArtCode] & "," & ORr.SerNr;
					end;
					quantShip[ORrw.ArtCode & ORr.SerNr] = ORrw.Quant-ORrw.Shipd2;
					rownr[ORrw.ArtCode & ORr.SerNr] = i;
					custCode[ORrw.ArtCode & ORr.SerNr] = ORr.CustCode;
					custName[ORrw.ArtCode & ORr.SerNr] =	ORr.Addr0;
					OrdDate[ORrw.ArtCode & ORr.SerNr] = ORr.OrdDate;
					reservDate[ORrw.ArtCode & ORr.SerNr] = ORr.EndReserv;
				end;
			end;
		end;
	end;
	startreportnoheaderjob("Reserved Report");
		outstring(0,0,"Заказ",false);
		outstring(50,0,"Резерв",false);
		outstring(100,0,"Стр.",false);
		outstring(150,0,"Клиент",false);
		outstring(200,0,"Имя клиента",false);
		outstring(300,0,"Дата заказа",false);
		outstring(350,0,"Резерв до",false);
	getvectortags(orders,tags);
	for(i=0;i<tags.length;i=i+1) begin
		itemall = 0;
		pos = 0;
		ExtractObj(orders[tags[i]],pos,orderNr);
		startformat(15);
				outstring(0,0, tags[i],false);
			endformat;
		while(nonblank(orderNr)) begin
			itemall = itemall + quantShip[tags[i] & orderNr];
			startformat(15);
				outstring(0,0,orderNr,false);
				outstring(50,0,quantShip[tags[i] & orderNr],false);
				outstring(100,0,rownr[tags[i] & orderNr],false);
				outstring(150,0,custCode[tags[i] & orderNr],false);
				outstring(200,0,custName[tags[i] & orderNr],false);
				outstring(300,0,OrdDate[tags[i] & orderNr],false);
				outstring(350,0,reservDate[tags[i] & orderNr],false);
			endformat;
			ExtractObj(orders[tags[i]],pos,orderNr);
		end;
		startformat(15);
				outstring(0,0,"Всего",false);
				outstring(50,0,itemall,false);
			endformat;
	end;
  EndJob;
	LogProcTime("ItemLocationStatusRn",getcurtick() - curtick);
  return;
end;

global procedure ReserveCheckRn(record RcVc RepSpec)
begin
	record ORVc ORr;
	row ORVc ORrw;
	boolean TrHs,testf;
	integer  mtrw,i;
	vector val resqty;
	record INVc INr;
	record ItemStatusVc ISr;
	vector string 100 altcode;
	longint curtick;
	ORr.Reserved = 1;
	
	curtick = getcurtick();
	startreportnoheaderjob("Резервы");
	
	startformat(15);
		outstring(0,0,"Товар",false);
		outstring(100,0,"Резерв",false);
		outstring(150,0,"Заказ",false);
		outstring(175,0,"Стр.",false);
		outstring(200,0,"Код производителя",false);
	endformat;
	
	TrHs = true;
	while(loopkey("Reserved",ORr,1,TrHs))begin
		testf = true;
		if(ORr.Reserved==0)then begin TrHs = false; testf = false; end;
		if(ORr.Closed>0)then begin testf = false; end;
		
		if(testf)then begin
			mtrw = matrowcnt(ORr);
			For(i=0;i<mtrw;i=i+1) begin
				matrowget(ORr,i,ORrw);
				if(nonblank(ORrw.ArtCode))then begin
					if(ORrw.Quant>0)then begin
						if(ORrw.Quant>ORrw.Shipd2)then begin
							resqty[ORrw.ArtCode] = resqty[ORrw.ArtCode] + (ORrw.Quant-ORrw.Shipd2);
							if(blank(altcode[ORrw.ArtCode]))then begin
								altcode[ORrw.ArtCode] = "_";
								INr.Code = ORrw.ArtCode;
								if(readfirstmain(INr,1,true))then begin
									altcode[ORrw.ArtCode] = INr.AlternativeCode;
								end;
							end;
							
							startformat(15);
								outstring(0,"DblINVc",ORrw.ArtCode,false);
								outstring(100,0,ORrw.Quant-ORrw.Shipd2,false);
								outstring(150,"DblORVc",ORr.SerNr,false);
								outstring(175,0,i+1,false);
								outstring(200,0,altcode[ORrw.ArtCode],false);
							endformat;
						end;
					end;
				end;	  
			end; 
		
		end;
	
	end;
	
	Gray_Divider(0,1);
	
	INr.Code = "";
	while(loopmain(INr,1,true))begin
		ISr.Code = INr.Code;
		ISr.Location = ";;;";
		readfirstmain(ISr,2,true);
		if(ISr.RsrvQty!=0 or resqty[INr.Code]!=0)then begin
			if(ISr.RsrvQty!=resqty[INr.Code] or ISr.RsrvQty>ISr.OrddOut)then begin
				startformat(15);
					outstring(0,0,INr.Code,false);
					outstring(100,0,ISr.RsrvQty,false);
					outstring(150,0,resqty[INr.Code],false);
					outstring(200,0,ISr.RsrvQty - resqty[INr.Code],false);
				endformat;
			end;
		end;
	end;
	
	
	endjob;
	 LogProcTime("ReserveCheckRn",getcurtick() - curtick);
return;
end;