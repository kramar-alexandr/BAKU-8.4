remote procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
remote procedure CntPOSCurrencies(var Array string,var Array Boolean,var Integer);


global procedure MiddleInvoiceRn(record RcVc RepSpec)
begin
	record IVVc IVr;
	row IVVc IVrw;
	integer i,mtrw;
	boolean TrHs,testf,first;
	string 50 key;
	integer fl;
	record CYBlock CYb;
	val day,total,dayqty,totalqty;
	integer invday,invtotal;
	date curdat;
	val fr,to1,to2,br1,br2,ivfr,ivto;
  string 20 curncy;
	Array string 50 acrncy;// Edit ************************** Tuesday, 19 March 2013 15:48:06
  Array Boolean achangecrncyf;// Edit ************************** Tuesday, 19 March 2013 15:48:52
  Integer acrncnt;// Edit ************************** Tuesday, 19 March 2013 15:48:51
  
	CntPOSCurrencies(acrncy,achangecrncyf,acrncnt);// Edit ************************** Wednesday, 19 June 2013 13:59:15
	curncy = acrncy[0];// Edit ************************** Wednesday, 19 June 2013 13:59:14
  
	
	blockload(CYb);
	
	startreportnoheaderjob(USetStr(31217));
	
	startformat(15);
		outstring(0,0,"",false);
		outstring(100,0,USetStr(31201),false);
		outstring(150,0,RepSpec.sStartDate & " : " & RepSpec.sEndDate,false);
	endformat;
	startformat(15);
	endformat;
	startformat(15);
		outstring(0,0,getyear(RepSpec.sStartDate),false);
		outstring(50,0,"",false);
		outstring(100,0,CYb.BusinessName,false);
		outstring(150,0,RepSpec.f1,false);
		outstring(190,0,USetStr(31218),false);
		if(nonblank(RepSpec.f2))then begin
			outstring(240,0,RepSpec.f2,false);
			outstring(300,0,USetStr(31219),false);
		end;
	endformat;
	black_divider(0,1);
	startformat(15);
	endformat;
	startformat(15);//Edit -----Dima-----------09.12.2014
		outstring(0,0,USetStr(31220),false);	//date
		outstring(50,0,USetStr(31221),false);	//quantity of receipts
		outstring(120,0,USetStr(31222),false);	//in total
		outstring(170,0,USetStr(31223),false);	//check's average cost
		outstring(240,0,USetStr(31224),false);	//total quantity per day 
	endformat;
	startformat(15);
	endformat;
	
	key = "TransDate";
	fl = 1;
	if(nonblank(RepSpec.f1))then begin
		key = "MachineName";
		IVr.MachineName = RepSpec.f1;
		fl = 2;
	end;
	
	IVr.TransDate = RepSpec.sStartDate;
	TrHs = true;
	curdat = stringtodate("1/1/2000");
	day = 0;
	dayqty = 0;
	total = 0;
	totalqty = 0;
	invday = 0;
	invtotal = 0;
	first = true;
	while(loopkey(key,IVr,fl,TrHs))begin
		if(first)then begin
			curdat = IVr.TransDate;
			first = false;
		end;
		if(curdat!=IVr.TransDate and invday>0)then begin
			startformat(15);
				outstring(0,0,curdat,false);
				outstring(50,0,invday,false);
				outstring(120,0,day,false);
				outstring(170,0,day/invday,false);
				outstring(240,0,dayqty,false);
			endformat;
			invday = 0;
			day = 0;
			dayqty = 0;
			curdat = IVr.TransDate;
		end;
		testf = true;
		if(IVr.TransDate<RepSpec.sStartDate or IVr.TransDate>RepSpec.sEndDate)then begin TrHs = false; testf = false; end;
		if(nonblank(RepSpec.f1) and IVr.MachineName!=RepSpec.f1)then begin 
			testf = false;
			if(key=="MachineName")then begin
				TrHs = false;
			end;
		end;
		if(nonblank(RepSpec.f2) and RepSpec.f2!=IVr.SalesMan)then begin testf = false; end;
		if(IVr.OKFlag==0)then begin testf = false; end;
		if(IVr.Invalid==1)then begin testf = false; end;
		//if(IVr.Sum4<0)then begin testf = false; end;
		
		if(testf)then begin
			ivfr = 1; ivto = 1;
			fr = 1; to1 = 1;
			if(IVr.CurncyCode!=curncy)then begin
				ivfr = IVr.FrRate;
				ivto = IVr.ToRateB1;
				if(ivfr==0 or ivto==0)then begin
					ivfr = 1;	ivto = 1;
				end;
				GetFullCurncyRate(curncy,IVr.TransDate,fr,to1,to2,br1,br2);
				if(fr==0 or to1==0)then begin
					fr = 1;	to1 = 1;
				end;
			end;
			invday = invday + 1;
			invtotal = invtotal + 1;
			day = day + IVr.Sum4/ivfr*ivto*fr/to1;
			
			mtrw = matrowcnt(IVr);			
			For(i=0;i<mtrw;i=i+1) begin
	  		matrowget(IVr,i,IVrw);
	  		if(IVrw.stp==kInvoiceRowTypeNormal and IVrw.Quant!=0)then begin
	  			dayqty = dayqty + 1;
	  			totalqty = totalqty + 1;
	  		end;
			end; 
			
			total = total + IVr.Sum4/ivfr*ivto*fr/to1;
		end;
	end;
	if(invday>0)then begin
		startformat(15);
			outstring(0,0,curdat,false);
			outstring(50,0,invday,false);
			outstring(120,0,day,false);
			outstring(170,0,day/invday,false);
			outstring(240,0,dayqty,false);
		endformat;
		invday = 0;
		day = 0;
		dayqty = 0;
		curdat = IVr.TransDate;
	end;
	gray_divider(0,1);
	startformat(15);
	endformat;
	startformat(15);
		outstring(0,0,USetStr(31211),false);
		outstring(50,0,invtotal,false);
		outstring(120,0,total,false);
		outstring(170,0,total/invtotal,false);
		outstring(240,0,totalqty,false);
	endformat;
	
	endjob;

return;
end;