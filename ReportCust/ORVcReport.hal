external procedure ExtractObj(string,var Integer,var string);
external procedure ExtractObjWithSeparator(string,string,Boolean,var Integer,var string);
external procedure LogProcTime(string,longint);

SetLangMode(LangRussian,"RUS",0);

global
procedure	ORVcRepRn(record RcVc RepSpec)
begin
	Integer 			mtrw, i, j, k;
	val					res;
	row ORVc			ORrw;
	vector val			diff;
	array string 255 	atags;
	record INVc			INr;
	Boolean				testf, TrHs;
	string	255			temp;
	record ORVc			ORr;
	record DIVc			DIr;
	record ItemStatusVc ISr;
	string 255			brand;
	string 255			colour;
	string 255 			size;
	string 255			name;
	string 255 			location;
	string 255			unit;
	val					qt;
	longint curtick;
	
	curtick = getcurtick();
	location = RepSpec.f1;
	StartReportJob("Report about additional purchases");
	Header(1,"Purchases",1);
	EndHeader;
	StartFormat(15);
	OutString(0,0,"Код",false);
	OutString(50,0,"Имя",false);
	OutString(190,0,"Бренд",false);
	OutString(240,0,"Размер",false);
	OutString(280,0,"Цвет",false);
	OutString(320,0,"Ед. измер.",false);
	OutString(360,0,"Склад",false);
	OutString(410,0,"Закупить",false);
	OutString(430,0,"Кол-во",false);
	OutString(450,0,"Не отгруж.",false);
	EndFormat;
	TrHs = true;
	testf = true;
	while(loopmain(ORr,1,TrHs)) begin
		testf = true;
		if (nonblank(location) and SetInSet(ORr.Location,location)==false) then begin
			testf = false;
		end;
		if (nonblank(RepSpec.f3) and SetInSet(ORr.OrderClass,RepSpec.f3)==false) then begin // Edit Dima ************* 2018-02-14 12:42 PM
			testf = false;
		end;		
		if (testf) then begin
			mtrw = matrowcnt(ORr);
			for (i = 0; i < mtrw; i = i + 1) begin
				matrowget(ORr,i,ORrw);
				if (nonblank(ORrw.ArtCode)) then begin
					res = ORrw.Invd - ORrw.Shipd2;
					if (res > 0) then begin
						diff[ORrw.ArtCode] = diff[ORrw.ArtCode] + res;
					end;
				end;
			end;
		end;
	end;
	GetVectorTags(diff,atags);
	for (i = 0; i < atags.length; i=i+1) begin
		testf = true;
		INr.Code = atags[i];
		if (readfirstmain(INr,1,true)) then begin
			if (nonblank(RepSpec.f2) and (SetInSet(RepSpec.f2,INr.DispGroups)==false)) then begin
				testf = false;
			end;			
			if (blank(location)) then begin
				location = ";;;";
			end;
			k = 0;
			qt = 0;
			ExtractObjWithSeparator(",",location,true,k,temp);
			while (nonblank(temp)) begin
				ISr.Code = INr.Code;
				ISr.Location = temp;
				if (readfirstmain(ISr,2,true)) then begin
					qt = qt + ISr.Instock;
				end;
				ExtractObjWithSeparator(",",location,true,k,temp);
			end;
			if (qt >= diff[atags[i]]) then begin
				testf = false;
			end;
			if (location==";;;") then begin
				location = "";
			end;
			if (testf) then begin
				StartFormat(15);
				OutString(0,"DblINVc",atags[i],false);
				OutString(50,0,INr.Name,false);
				j = 0;
				ExtractObj(INr.DispGroups,j,temp);
				brand = "";
				while (nonblank(temp)) begin
					DIr.Code = temp;
					if (readfirstmain(DIr,1,true)) then begin
						if (DIr.CType=="BRAND") then begin
							brand = DIr.Name;
						end;
						if (DIr.CType=="SIZE") then begin
							size = DIr.Name;
						end;
						if (DIr.CType=="COLOUR") then begin
							colour = DIr.Name;
						end;
					end;
					ExtractObj(INr.DispGroups,j,temp); 
				end;
				unit = INr.Unittext;
				OutString(190,0,brand,false);
				OutString(240,0,size,false);
				OutString(280,0,colour,false);
				OutString(320,0,unit,false);
				OutString(360,0,location,false);
				OutVal(410,0,diff[atags[i]] - qt,M4Val,false);
				OutVal(430,0,qt,M4Val,false);
				OutVal(450,0,diff[atags[i]],M4Val,false);
				EndFormat;
			end;
		end;
	end;
	EndJob;
	LogProcTime("ORVcRepRn",getcurtick() - curtick);
end;